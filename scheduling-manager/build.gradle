plugins {
    id "java"
    id "io.spring.dependency-management" version "1.0.3.RELEASE"
}

description "Scheduling lifecycle and deployment logic"

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'com.amazonaws:aws-java-sdk-bom:1.11.180'
        mavenBom 'software.amazon.awssdk:bom:2.0.0-preview-4'
    }
    dependencies {
        dependencySet(group: 'org.springframework', version: '4.3.10.RELEASE') {
            entry 'spring-core'
            entry 'spring-beans'
            entry 'spring-context'
            entry 'spring-test'
        }
    }
}

dependencies {
    compile(
            'com.amazonaws:aws-lambda-java-core:1.1.0',
            'com.amazonaws:aws-lambda-java-events:1.3.0',
            'com.amazonaws:aws-lambda-java-log4j2:1.0.0',

            'software.amazon.awssdk:ecs',
            'software.amazon.awssdk:lambda',

            'org.apache.commons:commons-lang3:3.6+',

            'org.projectlombok:lombok:1.16.18',

            'org.apache.logging.log4j:log4j-slf4j-impl:2.8.+',
            'org.slf4j:slf4j-api:1.7.+',

            'org.springframework:spring-core',
            'org.springframework:spring-beans',
            'org.springframework:spring-context',

            'com.spotify:completable-futures:+',
            project(":data-service-model"),
    )

    testCompile(
            'software.amazon.awssdk:cloudformation',

            'org.springframework:spring-test',
            'org.mockito:mockito-core:2.8+',
            'org.hamcrest:hamcrest-junit:2.+',
            'junit:junit:4.12'
    )
}

test {
    useJUnit {
        excludeCategories 'com.amazonaws.blox.testcategories.IntegrationTest'
    }
}

task buildZip(type: Zip) {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtime
    }
}

build.dependsOn buildZip
