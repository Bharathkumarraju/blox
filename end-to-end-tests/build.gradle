import com.amazonaws.blox.tasks.EndToEndTest
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id "java"
    id "blox-deploy"
    id 'com.github.johnrengelman.shadow' version '2.0.2'
}

sourceCompatibility = 1.8

dependencies {
    compile project(":frontend-service-client")

    compile "software.amazon.awssdk:ecs:2.0.0-preview-4"
    compile "software.amazon.awssdk:cloudformation:2.0.0-preview-4"
    compile "org.projectlombok:lombok:1.16.18"

    testCompile "org.assertj:assertj-core:3.8.0"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

test { onlyIf { false } }

deployment {
    aws {
        profile stack.profile.toString()
        region stack.region.toString()
    }

    stackName "blox-test-cluster"
    s3Bucket stack.s3Bucket.toString()

    templateFile file("templates/test_cluster.yml")
}

task testJar(type: ShadowJar) {
    group "build"
    description "Generate uber jar for end to end testing"
    classifier = 'tests'
    from sourceSets.test.output
    from sourceSets.main.output
    configurations = [project.configurations.testRuntime]
}

task testEndToEnd(type: EndToEndTest) {
    group "verification"
    description "Run end to end integration tests"

    setDefaultRegion stack.region.toString()
    setDefaultProfile stack.profile.toString()
    setDefaultEndpoint findProject(":frontend-service").deployment.outputs["ApiUrl"]
}

testEndToEnd.mustRunAfter "deploy"
